{
 "cells": [
  {
   "cell_type": "markdown",
   "id": "7af4788b-d496-4b3c-a14a-003e97b40315",
   "metadata": {},
   "source": [
    "# Example of multithreading in Julia\n",
    "\n",
    "In this example we compute the complex correlation coefficient of two velocity time series (u1,v1) and (u2,v2). We use this example to improve the performance of julia code"
   ]
  },
  {
   "cell_type": "code",
   "execution_count": 1,
   "id": "939d525d-53eb-4ce9-927a-497c75b09f0d",
   "metadata": {},
   "outputs": [],
   "source": [
    "using Base.Threads\n",
    "using BenchmarkTools\n",
    "using Test"
   ]
  },
  {
   "cell_type": "markdown",
   "id": "acf181e1-65f3-47f3-85de-940199112ee8",
   "metadata": {},
   "source": [
    "Following the instructions [here](https://github.com/gher-uliege/Documentation/wiki/Julia-Jupyter-notebook-with-multiple-threads) to create a julia kernel multiple threads.\n"
   ]
  },
  {
   "cell_type": "markdown",
   "id": "95b03d83-ad3e-4da2-98ef-314f37e22f44",
   "metadata": {},
   "source": [
    "`Threads.nthreads()` should not be 1."
   ]
  },
  {
   "cell_type": "code",
   "execution_count": 2,
   "id": "57b8a6da-44b4-4cfa-857a-6f704a03c276",
   "metadata": {},
   "outputs": [
    {
     "name": "stdout",
     "output_type": "stream",
     "text": [
      "Threads.nthreads() = 8\n"
     ]
    },
    {
     "data": {
      "text/plain": [
       "8"
      ]
     },
     "execution_count": 2,
     "metadata": {},
     "output_type": "execute_result"
    }
   ],
   "source": [
    "@show Threads.nthreads()"
   ]
  },
  {
   "cell_type": "markdown",
   "id": "561f4ddf-cda6-4594-8d99-04982a1f58cc",
   "metadata": {},
   "source": [
    "A \"native\" implementation to compute the complex correlation coefficient of two velocity time series (u1,v1) and (u2,v2) following the vectorized approach from numpy and matlab."
   ]
  },
  {
   "cell_type": "code",
   "execution_count": 3,
   "id": "a6cec774-bfd4-42cf-a683-dea95c4302e0",
   "metadata": {},
   "outputs": [
    {
     "data": {
      "text/plain": [
       "complex_correlation (generic function with 1 method)"
      ]
     },
     "execution_count": 3,
     "metadata": {},
     "output_type": "execute_result"
    }
   ],
   "source": [
    "function complex_correlation(u1,v1,u2,v2)\n",
    "    w1 = u1 + im * v1;\n",
    "    w2 = u2 + im * v2;\n",
    "    \n",
    "    gamma = w1' * w2 / sqrt( (w1'*w1) * (w2'*w2) );\n",
    "    mag = abs(gamma);\n",
    "    phi = angle(gamma)*180/pi;\n",
    "    return mag, phi\n",
    "end\n"
   ]
  },
  {
   "cell_type": "markdown",
   "id": "fe12a272-e431-47e3-b17a-82c0b7ffcb71",
   "metadata": {},
   "source": [
    "Let's check that `complex_correlation` is correct. A vector time series should correlate perfectly with itself:"
   ]
  },
  {
   "cell_type": "code",
   "execution_count": 4,
   "id": "0ba25733-eb39-4b1e-b6e6-82d168b46322",
   "metadata": {},
   "outputs": [
    {
     "data": {
      "text/plain": [
       "\u001b[32m\u001b[1mTest Passed\u001b[22m\u001b[39m"
      ]
     },
     "execution_count": 4,
     "metadata": {},
     "output_type": "execute_result"
    }
   ],
   "source": [
    "u1 = randn(100);\n",
    "v1 = randn(100);\n",
    "u2 = u1\n",
    "v2 = v1\n",
    "mag, phi = complex_correlation(u1,v1,u2,v2)\n",
    "\n",
    "@test mag ≈ 1\n",
    "@test phi ≈ 0"
   ]
  },
  {
   "cell_type": "markdown",
   "id": "cb073740-b681-4ebd-9b93-9d9eede679ff",
   "metadata": {},
   "source": [
    "If we apply a rotation of 20° (https://en.wikipedia.org/wiki/Rotation_matrix)"
   ]
  },
  {
   "cell_type": "code",
   "execution_count": 5,
   "id": "f07d19ba-c28e-41ef-9a24-d2a10140c3bb",
   "metadata": {},
   "outputs": [
    {
     "data": {
      "text/plain": [
       "\u001b[32m\u001b[1mTest Passed\u001b[22m\u001b[39m"
      ]
     },
     "execution_count": 5,
     "metadata": {},
     "output_type": "execute_result"
    }
   ],
   "source": [
    "u1 = randn(100);\n",
    "v1 = randn(100);\n",
    "u2 = u1 * cosd(20) - v1 * sind(20);\n",
    "v2 = u1 * sind(20) + v1 * cosd(20);\n",
    "mag, phi = complex_correlation(u1,v1,u2,v2)\n",
    "\n",
    "@test mag ≈ 1\n",
    "@test phi ≈ 20"
   ]
  },
  {
   "cell_type": "markdown",
   "id": "eea1f2b9-99d0-4767-a57a-74e371a9bacb",
   "metadata": {},
   "source": [
    "We still have a perfect correlation magnitude of 1 and we recover the rotation of 20°."
   ]
  },
  {
   "cell_type": "markdown",
   "id": "3f5f2fef-b434-47ae-ae1c-3095a674bee5",
   "metadata": {},
   "source": [
    "Make a large random dataset:"
   ]
  },
  {
   "cell_type": "code",
   "execution_count": 6,
   "id": "d82c2bfe-f5b0-4642-bfdb-eb903472c4ac",
   "metadata": {},
   "outputs": [
    {
     "name": "stdout",
     "output_type": "stream",
     "text": [
      "  0.442955 seconds (466.66 k allocations: 929.865 MiB, 16.71% gc time, 20.07% compilation time)\n"
     ]
    }
   ],
   "source": [
    "u1 = randn(100,100,1000);\n",
    "v1 = randn(100,100,1000);\n",
    "u2 = randn(100,100,1000);\n",
    "v2 = randn(100,100,1000);\n",
    "mag = zeros(size(u1)[1:2]);\n",
    "phi = zeros(size(u1)[1:2]);\n",
    "\n",
    "@time for j = 1:size(u1,2)\n",
    "  for i = 1:size(u1,1)\n",
    "     mag[i,j],phi[i,j] = complex_correlation(u1[i,j,:],v1[i,j,:],u2[i,j,:],v2[i,j,:])\n",
    "    end\n",
    "end"
   ]
  },
  {
   "cell_type": "markdown",
   "id": "a3066ffb-4b90-40cf-8da4-5e7bbdd0ad8a",
   "metadata": {},
   "source": [
    "For performance critical code, we should wrap the loops inside a function. Also the macro `@btime` is better for performance measurements:"
   ]
  },
  {
   "cell_type": "code",
   "execution_count": 7,
   "id": "e02d6f59-3798-4c50-93bd-8b747c285f33",
   "metadata": {},
   "outputs": [
    {
     "data": {
      "text/plain": [
       "compute_complex_correlation! (generic function with 1 method)"
      ]
     },
     "execution_count": 7,
     "metadata": {},
     "output_type": "execute_result"
    }
   ],
   "source": [
    "function compute_complex_correlation!(mag,phi,u1,v1,u2,v2)\n",
    "   for j = 1:size(u1,2)\n",
    "     for i = 1:size(u1,1)\n",
    "         mag[i,j],phi[i,j] = complex_correlation(u1[i,j,:],v1[i,j,:],u2[i,j,:],v2[i,j,:])\n",
    "     end\n",
    "   end\n",
    "end"
   ]
  },
  {
   "cell_type": "code",
   "execution_count": 8,
   "id": "4223086b-2ede-40f8-97cd-788205a6d365",
   "metadata": {},
   "outputs": [
    {
     "name": "stdout",
     "output_type": "stream",
     "text": [
      "  221.507 ms (240000 allocations: 921.02 MiB)\n"
     ]
    }
   ],
   "source": [
    " @btime compute_complex_correlation!(mag,phi,u1,v1,u2,v2)"
   ]
  },
  {
   "cell_type": "markdown",
   "id": "74119c38-b314-4bb9-9724-7da6ee4ce2fe",
   "metadata": {},
   "source": [
    "This simple change already improved quite a bit the runtime.\n",
    "Let's also check that the code is type stable by using `@code_warntype`:"
   ]
  },
  {
   "cell_type": "code",
   "execution_count": 9,
   "id": "8b7edf38-f416-4ed4-9e4d-5eaf8ef066ac",
   "metadata": {},
   "outputs": [],
   "source": [
    "#@code_warntype complex_correlation(u1[1,1,:],v1[1,1,:],u2[1,1,:],v2[1,1,:]);"
   ]
  },
  {
   "cell_type": "code",
   "execution_count": 10,
   "id": "d0f364a4-ccc7-4d01-98bc-7f60140a96f5",
   "metadata": {},
   "outputs": [],
   "source": [
    "#@code_warntype compute_complex_correlation!(mag,phi,u1,v1,u2,v2);"
   ]
  },
  {
   "cell_type": "markdown",
   "id": "336fb803-4586-4651-8ebb-c706984b3a25",
   "metadata": {},
   "source": [
    "OK, the code is type stable: all types are concrete (like `Int64`, `Vector{Float64}` shown in blue, or small unions like `Union{Nothing, Tuple{Int64, Int64}}` shown in yellow. There is no `Any` that would be shown in red. \n",
    "\n",
    "Note that we have a lot of allocation that we can reduce using `@view`:"
   ]
  },
  {
   "cell_type": "code",
   "execution_count": 18,
   "id": "c3523b6c-7ceb-4403-b668-3e5b9310ef7e",
   "metadata": {},
   "outputs": [
    {
     "data": {
      "text/plain": [
       "compute_complex_correlation! (generic function with 1 method)"
      ]
     },
     "execution_count": 18,
     "metadata": {},
     "output_type": "execute_result"
    }
   ],
   "source": [
    "function compute_complex_correlation!(mag,phi,u1,v1,u2,v2)\n",
    "   for j = 1:size(u1,2)\n",
    "     for i = 1:size(u1,1)\n",
    "         mag[i,j],phi[i,j] = complex_correlation((@view u1[i,j,:]),(@view v1[i,j,:]),(@view u2[i,j,:]),(@view v2[i,j,:]))\n",
    "     end\n",
    "   end\n",
    "end"
   ]
  },
  {
   "cell_type": "code",
   "execution_count": 12,
   "id": "a35613fc-4ed8-4307-a8c1-dfdd69626784",
   "metadata": {},
   "outputs": [
    {
     "name": "stdout",
     "output_type": "stream",
     "text": [
      "  177.958 ms (120000 allocations: 613.10 MiB)\n"
     ]
    }
   ],
   "source": [
    "@btime compute_complex_correlation!(mag,phi,u1,v1,u2,v2)"
   ]
  },
  {
   "cell_type": "markdown",
   "id": "12b8bd51-9bdb-4334-a78f-bfd3bbc3c48d",
   "metadata": {},
   "source": [
    "In fact, also `complex_correlation` does a lot of allocations (when computing a new vector `u1 + im * v2` for example) that can be avoided by making an explicit loop:"
   ]
  },
  {
   "cell_type": "code",
   "execution_count": 20,
   "id": "0a2bff6b-4cfe-4873-a5ee-ecc68b9e6138",
   "metadata": {},
   "outputs": [
    {
     "data": {
      "text/plain": [
       "complex_correlation (generic function with 1 method)"
      ]
     },
     "execution_count": 20,
     "metadata": {},
     "output_type": "execute_result"
    }
   ],
   "source": [
    "function complex_correlation(u1,v1,u2,v2)\n",
    "    w1w2 = 0.0 + im * 0.0\n",
    "    w1w1 = 0.0\n",
    "    w2w2 = 0.0    \n",
    "    \n",
    "    for n = 1:length(u1)\n",
    "        w1 = u1[n] + im * v1[n]\n",
    "        w2 = u2[n] + im * v2[n]\n",
    "        w1w2 += w1' * w2\n",
    "        w1w1 += w1' * w1 # or abs2(w1)\n",
    "        w2w2 += w2' * w2\n",
    "    end\n",
    "    \n",
    "    gamma = w1w2 / sqrt(w1w1 * w2w2);\n",
    "    mag = abs(gamma);\n",
    "    phi = angle(gamma)*180/pi;\n",
    "    return mag, phi\n",
    "end"
   ]
  },
  {
   "cell_type": "markdown",
   "id": "305ead8b-58c7-459e-9413-20d8899080e5",
   "metadata": {},
   "source": [
    "Optimization can introduce bugs, so lets check that the results is still ok:"
   ]
  },
  {
   "cell_type": "code",
   "execution_count": 21,
   "id": "0d95109a-91bc-4dec-bfbc-7d43a76f449d",
   "metadata": {},
   "outputs": [
    {
     "data": {
      "text/plain": [
       "\u001b[32m\u001b[1mTest Passed\u001b[22m\u001b[39m"
      ]
     },
     "execution_count": 21,
     "metadata": {},
     "output_type": "execute_result"
    }
   ],
   "source": [
    "\n",
    "u1 = randn(100);\n",
    "v1 = randn(100);\n",
    "u2 = u1 * cosd(20) - v1 * sind(20);\n",
    "v2 = u1 * sind(20) + v1 * cosd(20);\n",
    "mag, phi = complex_correlation(u1,v1,u2,v2)\n",
    "\n",
    "@test mag ≈ 1\n",
    "@test phi ≈ 20"
   ]
  },
  {
   "cell_type": "code",
   "execution_count": 22,
   "id": "fc8c3aae-dda8-435a-81a1-4138a952ef91",
   "metadata": {},
   "outputs": [
    {
     "name": "stdout",
     "output_type": "stream",
     "text": [
      "  114.653 ms (0 allocations: 0 bytes)\n"
     ]
    }
   ],
   "source": [
    "u1 = randn(100,100,1000);\n",
    "v1 = randn(100,100,1000);\n",
    "u2 = randn(100,100,1000);\n",
    "v2 = randn(100,100,1000);\n",
    "mag = zeros(size(u1)[1:2]);\n",
    "phi = zeros(size(u1)[1:2]);\n",
    "@btime compute_complex_correlation!(mag,phi,u1,v1,u2,v2)"
   ]
  },
  {
   "cell_type": "markdown",
   "id": "c12749c7-12dc-4bb5-ad8b-864865307adf",
   "metadata": {},
   "source": [
    "Great! No allocations anymore. Sometimes it helps to disable the bounds checking by using `@inbounds` or use `abs2(w1)` instead of `w1' * w1` (but not here in my tests). Now the outer loop is parallelized using `Threads.@threads"
   ]
  },
  {
   "cell_type": "code",
   "execution_count": 23,
   "id": "955b46ef-4ba8-47e5-a08a-9b2efff44386",
   "metadata": {},
   "outputs": [
    {
     "data": {
      "text/plain": [
       "compute_complex_correlation! (generic function with 1 method)"
      ]
     },
     "execution_count": 23,
     "metadata": {},
     "output_type": "execute_result"
    }
   ],
   "source": [
    "function compute_complex_correlation!(mag,phi,u1,v1,u2,v2)\n",
    "   Threads.@threads for j = 1:size(u1,2)\n",
    "     for i = 1:size(u1,1)\n",
    "         mag[i,j],phi[i,j] = complex_correlation((@view u1[i,j,:]),(@view v1[i,j,:]),(@view u2[i,j,:]),(@view v2[i,j,:]))\n",
    "     end\n",
    "   end\n",
    "end"
   ]
  },
  {
   "cell_type": "code",
   "execution_count": 25,
   "id": "75633fe7-3d78-44fb-9f6b-7438312d7d9e",
   "metadata": {},
   "outputs": [
    {
     "name": "stdout",
     "output_type": "stream",
     "text": [
      "  22.378 ms (58 allocations: 4.50 KiB)\n"
     ]
    }
   ],
   "source": [
    "@btime compute_complex_correlation!(mag,phi,u1,v1,u2,v2)"
   ]
  },
  {
   "cell_type": "markdown",
   "id": "66f63cb4-0be6-4c87-a541-fd887e1d2a28",
   "metadata": {},
   "source": [
    "So we went down from ~420 ms to ~20 ms! Note that multithreading also introduce synchronization overhead."
   ]
  }
 ],
 "metadata": {
  "kernelspec": {
   "display_name": "Julia 8 Threads 1.12.1",
   "language": "julia",
   "name": "julia-8-threads-1.12"
  },
  "language_info": {
   "file_extension": ".jl",
   "mimetype": "application/julia",
   "name": "julia",
   "version": "1.12.1"
  }
 },
 "nbformat": 4,
 "nbformat_minor": 5
}
